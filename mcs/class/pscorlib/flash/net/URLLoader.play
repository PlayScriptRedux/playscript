// Copyright 2013 Zynga Inc.
//	
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//		
//      Unless required by applicable law or agreed to in writing, software
//      distributed under the License is distributed on an "AS IS" BASIS,
//      WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//      See the License for the specific language governing permissions and
//      limitations under the License.

package flash.net {

	import System.IO.*;
	import flash.events.EventDispatcher;
	import flash.events.Event;
	import flash.events.TimerEvent;
	import flash.events.IOErrorEvent;
	import flash.utils.*;
	import System.Net.*;

#if PLATFORM_MONODROID
	import Android.App.*;
#endif

	public class URLLoader extends EventDispatcher {

		//
		// Properties
		//
			
		public property bytesLoaded : uint { get; set; }

	 	public property bytesTotal : uint { get; set; }

 	 	public property data : * { get; set; }

 	 	public property dataFormat : String { get; set; }
 	 	
		//
		// Methods
		//

		public function URLLoader(request:URLRequest = null) {
			dataFormat = URLLoaderDataFormat.TEXT;
			mRequest = request;
		}


		public override function addEventListener(type:String, listener:Function, useCapture:Boolean = false, priority:int = 0, useWeakReference:Boolean = false):void {
			super.addEventListener(type, listener, useCapture, priority, useWeakReference);
		}
 	 	
		public function close():void {
			throw new System.NotImplementedException();
		}
 	 	
		public function load(request:URLRequest, synchronous:Boolean = false):void {
			mRequest = request;

			var url:String = request.url.ToLowerInvariant();
			if (url.StartsWith("http:") || url.StartsWith("https:")) {
				// handle a http load
				// load with a delay
				
				// Replace https: by http:
				if (mRequest.url.StartsWith("https:"))
				{
//					trace("Disable SSL for request " + mRequest.url);
					mRequest.url = mRequest.url.Replace("https:", "http:");
				}

				if (PlayScript.Player.Offline) {
					// load from offline cache
					if (synchronous) {
						doLoadFromOfflineCache();
					} else {
						// this must be done on the 'next' frame
						Timer.delayCall (doLoadFromOfflineCache);
					}
				} else 
				{
					if (synchronous) {
						doHttpLoadSynchronous();
					} else {
						doHttpLoadAsynchronous();
					}
				}

			} else {
				if (synchronous) {
					doLocalLoad();
				} else {
					// load with a delay
					Timer.delayCall (doLocalLoad);
				}
			}
		}

		// Temporary hack
		// TODO: Find a better place for this

		private function doHttpLoadSynchronous():void {
			var request:WebRequest = sendRequest();
			if (request) {
				var response = HttpWebResponse(request.GetResponse());
				responseAction(response);

				// Dispatch complete event at the end of the synchronous load
				dispatchCompleteEvent();
			}
		}

		private function doHttpLoadAsynchronous():void {
			// Note that data might not be sent asynchronously...
			var request:WebRequest = sendRequest();
			if (request) {
				// We are going to wait the response in another thread
				// And the complete event will be added in a delayed timer, so the event is dispatched in the main thread
				DoWithResponse(request, responseActionWithDelayedEvent);
			}
		}

		private function doLoadFromOfflineCache():void {
			var hash:String = mRequest.getHash();

			switch (dataFormat) 
			{
			case URLLoaderDataFormat.BINARY: 
				var cachedBinaryResponse:ByteArray = PlayScript.Player.LoadBinaryWebResponseFromCache(hash);
				if (cachedBinaryResponse != null) 
				{
					trace("URLLoader: http cached binary ", mRequest.url);
					
					this.bytesLoaded = cachedBinaryResponse.length;
					this.bytesTotal  = cachedBinaryResponse.length;
					this.data = cachedBinaryResponse;
					dispatchCompleteEvent();
					return;
				} 
				break;
			case URLLoaderDataFormat.TEXT: 
				var cachedTextResponse:String = PlayScript.Player.LoadTextWebResponseFromCache(hash);
				if (cachedTextResponse != null) 
				{
					trace("URLLoader: http cached text ", mRequest.url);
					
					this.bytesLoaded = cachedTextResponse.Length;
					this.bytesTotal  = cachedTextResponse.Length;
					this.data = cachedTextResponse;
					dispatchCompleteEvent();
					return;
				} 
				break;
			}
				
			// offline and no cached response, return error
			trace("URLLoader: http offline ", mRequest.url);
			dispatchErrorEvent();
		}

		private function sendRequest():WebRequest {
			trace("URLLoader: http load ", mRequest.url);

			var method:String = "GET";
			if (mRequest.method != null) {
				method = mRequest.method;
			}

			var wr:WebRequest;
			var url:String = mRequest.url;
			switch(method) {
				case "GET": {
					// Pass parameters using query parameters
					if (mRequest.data) {
						url += "?" + mRequest.data.toString();
					}
					wr = HttpWebRequest.Create(url);
					wr.Method = method;
					break;
				}
				default:
					wr = HttpWebRequest.Create(url);
					wr.Method = method;
					if (mRequest.data) {
						var postData = String(mRequest.data);
						var post = wr.GetRequestStream();
						var writer = new StreamWriter(post);
						writer.Write(postData);
						writer.Close();
					}
					break;
			}

			wr.ContentType="application/x-www-form-urlencoded";
			wr.Credentials = CredentialCache.DefaultCredentials;
			return wr;
		}

		private function responseAction(response:HttpWebResponse):void {
			var hash:String = mRequest.getHash();

			if (response.StatusCode != HttpStatusCode.OK) {
				// Display the status.
				trace("URLLoader: http load ", response.StatusDescription, mRequest.url);
			}

			// Get the stream containing content returned by the server.
			var dataStream = response.GetResponseStream ();

			// read response into byte array
			var ba:ByteArray = new ByteArray();
			ba.setCapacity(response.ContentLength);
			ba.readFromStream(dataStream);
			ba.position = 0;
			this.bytesLoaded = ba.length;
			this.bytesTotal  = ba.length;

			switch (dataFormat) {
				case URLLoaderDataFormat.BINARY: {
						this.data = ba;
						PlayScript.Player.StoreBinaryWebResponseIntoCache(hash, ba);
						break;
					}
				case URLLoaderDataFormat.TEXT: {
						// convert to string
						var text = ba.toString();
						this.data = text;
						PlayScript.Player.StoreTextWebResponseIntoCache(hash, text);
						break;
					}
				default:
					throw new System.NotImplementedException();
			}

			// Cleanup the streams and the response.
			dataStream.Close ();
			response.Close ();
			trace("URLLoader: http load complete length:", this.bytesLoaded, mRequest.url);
		}

		private function responseActionWithDelayedEvent(response:HttpWebResponse):void {
			responseAction(response);
			// By delaying the complete event, we make sure it is going to be dispatched in the main thread
			Timer.delayCall(dispatchCompleteEvent);
		}

		private function dispatchCompleteEvent():void {
			// TODO:addEventListener Find a way to make sure this event is dispatched in the main thread
			var de:Event = new Event(Event.COMPLETE);
			this.dispatchEvent(de);
		}

		private function dispatchErrorEvent():void {
			var derror:Event = new IOErrorEvent(IOErrorEvent.IO_ERROR, false, false, "offline");
			this.dispatchEvent(derror);
		}

		private function doLocalLoad():void {
			trace("URLLoader: local load ", mRequest.url);

			// handle file loading
			var path:String = PlayScript.Player.TryResolveResourcePath(mRequest.url);
			if (path == null)
			{
				trace("URLLoader: could not load ", mRequest.url);
				// io error, file not found
				this.dispatchEvent(new IOErrorEvent(IOErrorEvent.IO_ERROR));
				return;
			}

#if PLATFORM_MONOMAC || PLATFORM_MONOTOUCH
			// file exists. read it
			var fileData:byte[] = File.ReadAllBytes(path);
#elif PLATFORM_MONODROID 
			var dest:MemoryStream = new MemoryStream();
			var src:Stream = Application.Context.Assets.Open(path);
			src.CopyTo(dest);
			var fileData:byte[] = dest.ToArray();
#endif

			switch (dataFormat)
			{
			case URLLoaderDataFormat.BINARY: {
				var ba:ByteArray = ByteArray.fromArray(fileData);
				this.data = ba;
				break;
			}
			case URLLoaderDataFormat.TEXT: {
				var text:String =  System.Text.Encoding.UTF8.GetString(fileData);
				this.data = text;
				break;
			}
			default:
				throw new System.NotImplementedException();
			}

			// update bytes loaded
			this.bytesLoaded = fileData.Length;
			this.bytesTotal  = fileData.Length;

			// dispatch complete event
			var de:Event = new Event(Event.COMPLETE);
			this.dispatchEvent(de);
		}
		
		private var mRequest:URLRequest;
	}

}
